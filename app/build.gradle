apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 20200
        versionName "2.0.2"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "source"
    productFlavors {
        coolapk {
            applicationId "rikka.searchbyimage"
            buildConfigField "boolean", "hideOtherEngine", "false"
        }
        google {
            applicationId "rikka.searchbyimage2"
            versionNameSuffix "-play"
            buildConfigField "boolean", "hideOtherEngine", "true"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${variant.buildType.name.toLowerCase()}" + "-${variant.versionName}" + ".apk"
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    //https://developer.android.com/jetpack/androidx
    //TODO Utilize major changes in https://developer.android.com/jetpack/androidx/releases/appcompat#1.1.0
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //TODO: Utilize major changes in https://developer.android.com/jetpack/androidx/releases/browser#1.2.0
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    //https://github.com/bumptech/glide/releases
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    //https://square.github.io/okhttp/changelog_3x/
    implementation 'com.squareup.okhttp3:okhttp:3.6.0'

    //https://github.com/ReactiveX/RxJava/blob/1.x/CHANGES.md
    implementation 'io.reactivex:rxjava:1.2.6'
    //https://github.com/ReactiveX/RxAndroid/blob/1.x/CHANGES.md
    implementation 'io.reactivex:rxandroid:1.2.1'

    //https://github.com/ArthurHub/Android-Image-Cropper/releases
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.4.7'

    //https://github.com/tbruyelle/RxPermissions/releases
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'

    //https://github.com/RikkaW/MaterialPreference
    implementation 'com.github.RikkaW:MaterialPreference:v1.4.4'
}
