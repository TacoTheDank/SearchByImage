apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 25
        versionCode 20003
        versionName "2.0.3"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "source"
    productFlavors {
        coolapk {
            applicationId "rikka.searchbyimage"
            buildConfigField "boolean", "hideOtherEngine", "false"
        }
        google {
            applicationId "rikka.searchbyimage2"
            versionNameSuffix "-play"
            buildConfigField "boolean", "hideOtherEngine", "true"
        }
    }

    applicationVariants.all { variant ->
        outputs.all { output ->
            outputFileName = "${name.toLowerCase()}" + "-${versionName}" + ".apk"
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        disable 'GoogleAppIndexingWarning'
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    //https://developer.android.com/jetpack/androidx
    //TODO Utilize major changes in https://developer.android.com/jetpack/androidx/releases/appcompat#1.1.0
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //TODO: Utilize major changes in https://developer.android.com/jetpack/androidx/releases/browser#1.2.0
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    //TODO: Utilize major changes in 1.2.0 https://developer.android.com/jetpack/androidx/releases/core#version_120_3
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    //TODO: Utilize major changes in 1.1.0
    //https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.1.0'

    //https://github.com/bumptech/glide/releases
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //https://square.github.io/okhttp/changelog_3x/
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'

    //https://github.com/ReactiveX/RxJava/blob/2.x/CHANGES.md
    implementation 'io.reactivex:rxjava:1.3.8'
    //https://github.com/ReactiveX/RxAndroid/blob/2.x/CHANGES.md
    implementation 'io.reactivex:rxandroid:1.2.1'

    //https://github.com/ArthurHub/Android-Image-Cropper/releases
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    //https://github.com/tbruyelle/RxPermissions/releases
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'

    //https://github.com/RikkaW/MaterialPreference
    implementation 'com.github.RikkaW:MaterialPreference:v1.4.4'
}
